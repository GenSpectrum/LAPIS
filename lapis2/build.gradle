import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.8.20'
    id 'org.jlleitschuh.gradle.ktlint' version "11.3.1"
    id 'org.springdoc.openapi-gradle-plugin' version "1.6.0"
}

group = 'org.genspectrum'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '19'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: "org.mockito"
    }
    testImplementation 'com.ninja-squad:springmockk:4.0.2'
    testImplementation 'org.mock-server:mockserver-netty:5.15.0'
}

compileKotlin {
    compilerOptions {
        freeCompilerArgs.add("-Xexport-kdoc")
        jvmTarget.set(JvmTarget.JVM_19)
    }
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events TestLogEvent.FAILED, TestLogEvent.SKIPPED, TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
    }
}

tasks.named('bootBuildImage') {
    imageName = "ghcr.io/genspectrum/${project.name}"
}

// workaround related to https://github.com/springdoc/springdoc-openapi-gradle-plugin/issues/102
tasks.named("jar") {
    mustRunAfter("forkedSpringBootRun", "generateOpenApiDocs")
}

tasks.named("inspectClassesForKotlinIC") {
    mustRunAfter("forkedSpringBootRun", "generateOpenApiDocs")
}

openApi {
    outputDir.set(file("$rootDir"))
    outputFileName.set("lapis-v2-openapi.json")
    apiDocsUrl.set("http://localhost:8080/api-docs")
    customBootRun {
        args.set(["--silo.url=does.not.matter.here"])
    }
}
